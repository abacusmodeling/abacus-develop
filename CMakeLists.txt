########################################
# CMake build system
# This file is part of ABACUS
cmake_minimum_required(VERSION 3.18)
########################################

project(ABACUS
    VERSION 2.2.0
    DESCRIPTION "ABACUS is an electronic structure package based on DFT."
    HOMEPAGE_URL "https://github.com/deepmodeling/abacus-develop"
    LANGUAGES CXX
)
set(ABACUS_BIN_NAME ${PROJECT_NAME}-${PROJECT_VERSION})

set(CMAKE_CXX_STANDARD 11)
add_compile_options(-O2 -g)

set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/modules)
set(ABACUS_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/source)
set(ABACUS_TEST_DIR ${CMAKE_CURRENT_SOURCE_DIR}/tests)
include_directories(${ABACUS_SOURCE_DIR})

find_package(Boost REQUIRED)
find_package(Cereal REQUIRED)
find_package(ELPA REQUIRED)
find_package(MPI REQUIRED)
find_package(Threads REQUIRED)
find_package(OpenMP REQUIRED)

include_directories(
    ${Cereal_INCLUDE_DIRS}
    ${Boost_INCLUDE_DIRS}
    ${MPI_CXX_INCLUDE_PATH}
)

if(DEFINED MKL_DIR)
  find_package(MKL REQUIRED)
  add_definitions(-D__MKL -DMKL_ILP64)
  include_directories(${MKL_INCLUDE_DIRS} ${MKL_INCLUDE_DIRS}/fftw)
else()
  find_package(FFTW3 REQUIRED)
  find_package(LAPACK REQUIRED)
  find_package(ScaLAPACK REQUIRED)
  include_directories(${FFTW3_INCLUDE_DIRS})
endif()

add_definitions(
    -D__EXX
    -D__FFTW3
    -D__FP
    -D__MPI
    -D__OPENMP
    -D__SELINV
    -D__LCAO
    -DMETIS
    -DEXX_DM=3
    -DEXX_H_COMM=2
    -DTEST_EXX_LCAO=0
    -DTEST_EXX_RADIAL=1
    -DUSE_CEREAL_SERIALIZATION
)

add_subdirectory(source)

add_executable(${ABACUS_BIN_NAME}
    source/main.cpp
)

target_link_libraries(${ABACUS_BIN_NAME}
    base
    cell
    neighbor
    orb
    global
    io
    ions
    lcao
    parallel
    mrrr
    pdiag
    pw
    ri
    driver
    -lm
    ELPA::ELPA
    MPI::MPI_CXX
    OpenMP::OpenMP_CXX
    Threads::Threads
)

if(DEFINED MKL_DIR)
  target_link_libraries(${ABACUS_BIN_NAME}
    -lifcore
    MKL::MKL
    OpenMP::OpenMP_CXX
  )
else()
  target_link_libraries(${ABACUS_BIN_NAME}
    -lgfortran
    FFTW3::FFTW3
    ScaLAPACK::ScaLAPACK
    LAPACK::LAPACK
    OpenMP::OpenMP_CXX
  )
endif()

install(PROGRAMS ${CMAKE_CURRENT_BINARY_DIR}/${ABACUS_BIN_NAME}
    TYPE BIN
    DESTINATION ${CMAKE_INSTALL_BINDIR}
    RENAME ${PROJECT_NAME}
)

add_subdirectory(tests)
